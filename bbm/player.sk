function bbm_setMeta(p: player, k: text, v: object):
    set metadata "bbm_%{_k}%" of {_p} to {_v}
function bbm_getMeta(p: player, k: text) :: object:
    return metadata "bbm_%{_k}%" of {_p}
load:
    set {smallnumeric::*} to split "₁₂₃₄₅₆₇₈₉₀" at ""
function bbm_playerloop(p: player):
    set {_id} to metadata "playing_game" of {_p}
    set {_u} to {_p}'s uuid
    {node::%{_id}%::state} is 1:
        metadata "state" of {_p} is 0
        set {_bomb} to bbm_getMeta({_p}, "bomb")
        set {_bombtype} to bbm_getMeta({_p}, "bomb_type")
        set {_bomb_using} to bbm_getMeta({_p}, "bomb_using")
        set {_fire} to bbm_getMeta({_p}, "fire")
        set {_speed} to bbm_getMeta({_p}, "speed")
        set {_amount} to {_bomb} - {_bomb_using}
        set {_small} to {smallnumeric::%{_bomb}%}

        # gamemode
        {_p}'s gamemode is adventure:
            set {_p}'s gamemode to survival

        # speed
        set walk speed of {_p} to (0.15 + {_speed}*0.05)

        # checkpos
        set {_y} to bbm_mass({node::%{_id}%::pos})'s y-loc
        if ({_y}+2.5) <= {_p}'s y-loc:
            set {_pos} to bbm_getMeta({_p}, "pos")
            teleport {_p} to {game::%{_id}%::pos::%{_pos}%}
            send "&cボンバーマンステージの上に乗りあがるのはお止め下さい" to {_p}
            send "&7(同様の行為が繰り返し認められる場合、処罰の可能性があります)" to {_p}
            add 1 to {noriageman::%{_p}%}

        # actionbar
        set {_1} to "&b&l%{_amount}% &3/%{_small}%"
        set {_2} to {bbm_items::%{_bombtype}%}
        set {_3} to "&c火力 &l%{_fire}%"
        set {_4} to "&e速度 &l%{_speed}%"
        set {_actionbar} to "&7« ボム所持数 %{_1}% &7» &7<%{_2}%&7> &7| %{_3}%&7, %{_4}%"
        send action bar {_actionbar} to {_p}

        set slot 4 of {_p}'s inventory to bbm_getItem({_bombtype})
    else:
        set {_size} to size of {game::%{_id}%::Players::*}
        set {_maptype} to {game::%{_id}%::maptype}
        set {_max} to {bbm_playernum::%{_maptype}%::2}
        if {queue::%{_id}%} is set:
            set {_} to "&b&l%{queue::%{_id}%}% &b秒後に開始"
        else:
            set {_} to "&6(&l%{_size}% &6/ %{_max}%&6)"
        send action bar "&0«待機中: &c&lボンバーマン&7[%{_id}%]&0» %{_}%" to {_p}


place:
    player's gamemode is survival
    set {_p} to player
    metadata "state" of {_p} is 0
    metadata "playing_game" of {_p} is set
    cancel event
    set {_l} to location of event-block
    set {_i} to player's tool
    bbm_placeevent({_p}, {_l})
    bbm_pre_setbomb({_p}, {_l}, {_i})
function bbm_placeevent(p: player, l: location):
    add 1.5 to {_l}'s y-loc
    distance between location at block at {_l} and location of {_p} <= 1.73
    remove 1.5 from {_l}'s y-loc
    teleport {_p} to location at block at {_l}


left click:
    player's gamemode is survival
    event-block is set
    set {_p} to player
    metadata "state" of {_p} is 0
    metadata "playing_game" of {_p} is set
    cancel event

    set {_v} to vector from event-block and {_p}
    set standard length of {_v} to 0.1
    set {_v}'s y to -0.1
    add {_v} to {_p}'s velocity

    set {_mass} to bbm_mass(event-block's location)
    metadata "bbm_placed_bombholder" of block at {_mass} is set:
        set {_player} to metadata "bbm_placed_bombholder" of block at {_mass}
        set {_type} to metadata "bbm_placed_bombtype" of block at {_mass}
        set {_fire} to metadata "bbm_placed_bombfire" of block at {_mass}
        send "&7設置したプレイヤー: %{_player}%"
        send "&7ボムタイプ: %{_type}%"
        send "&7火力: %{_fire}%"


pick up:
    player's gamemode is survival
    set {_p} to player
    metadata "state" of {_p} is 0
    metadata "playing_game" of {_p} is set
    cancel event
    set {_type} to tag "bbm_item" of nbt of event-item
    delete event-dropped item

    bbm_addParam({_p}, {_type})


command /noriage:
    permission: op
    trigger:
        loop {noriageman::*}:
            send "%loop-index%: %loop-value%"

function bbm_addParam(p: player, type: text):
    if {_type} is "bombup" or "normal_bomb":
        play sound "entity.chicken.egg" with pitch 0.8 at {_p}
        set {_meta} to "bomb"
    else if {_type} is "fireup":
        play sound "block.anvil.land" with pitch 1.7 at {_p}
        set {_meta} to "fire" 
    else if {_type} is "speedup":
        play sound "entity.wither.shoot" with pitch 1.3 at {_p}
        set {_meta} to "speed" 
    set {_param} to bbm_getMeta({_p}, {_meta})
    set {_param} to min(5, {_param} + 1)
    bbm_setMeta({_p}, {_meta}, {_param})

function bbm_kill(v: player, a: player, type: text = "normal_bomb"):
    metadata "playing_game" of {_v} is metadata "playing_game" of {_a}
    set {_id} to metadata "playing_game" of {_v}
    set {_vu} to {_v}'s uuid
    set {_au} to {_a}'s uuid
    set {_bomb} to {bbm_items::%{_type}%}

    set {_p::*} to {game::%{_id}%::players::*} and {spec::%{_id}%::*}

    if {_v} is {_a}:
        send "&7%{_a}% &7--[%{_bomb}%&7]--> &c%{_a}% &7(自爆)" to {game::%{_id}%::players::*}
    else:
        send "&7%{_a}% &7--[%{_bomb}%&7]--> &c%{_v}%" to {game::%{_id}%::players::*}
        add 1 to {stat::%{_au}%::bbm::kill}
        add 1 to {stat::%{_vu}%::bbm::death}
    remove {_v} from {game::%{_id}%::players::*}
    set {spec::%{_id}%::*} to {_v}
    if bbm_check({_id}) is false:
        set {_size} to amount of {game::%{_id}%::players::*}
        send "&7- 残り %{_size}%人" to {game::%{_id}%::players::*}
        user_spectator({_v}, {_id})
    else:
        user_reset({_v})